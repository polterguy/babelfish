
/*
 * This is the endpoint you should use to retrieve all translations from your backend,
 * from your enterprise app, since it implements caching for 2 hours.
 */
.arguments
   locale.eq:string
   id.like:string
.description:Returns all translation entities from your backend for the given locale, matching the optional id


/*
 * Sanity checking invocation.
 */
validators.mandatory:x:@.arguments/*/locale.eq


/*
 * Making sure we cache items on client for 1200 seconds.
 */
response.headers.set
   Cache-Control:public, max-age=1200


/*
 * Connecting to database.
 */
data.connect:[generic|babelfish]
   add:x:+/*/where/*/and
      get-nodes:x:@.arguments/*


   /*
    * Reading items from database.
    */
   data.read
      table:translations
      columns
         id
         content
      limit:-1
      where
         and


   /*
    * Transforming result to key/value object.
    */
   for-each:x:@data.read/*
      set-name:x:./*/add/*/*
         get-value:x:@.dp/#/*/id
      set-value:x:./*/add/*/*
         get-value:x:@.dp/#/*/content
      add:x:@data.connect/*/return
         .
            .


   /*
    * Returning results of above invocation to caller.
    */
   return
